/**
 *	Royal Fun
 */
#Extends "Modes/ShootMania/Royal/RoyalBase.Script.txt"

#Const CompatibleMapTypes	"RoyalArena,RoyalArena2,LobbyArena"
#Const Version							"2017-08-29"
#Const ScriptName						"Modes/ShootMania/Royal/RoyalExp.Script.txt"

// ---------------------------------- //
// Libraries
// ---------------------------------- //
#Include "Libs/Nadeo/ShootMania/Fun/Manager.Script.txt" as FunMgr

// ---------------------------------- //
// Settings
// ---------------------------------- //
#Setting S_MapPointsLimit				200		as _("Points to win a map")
#Setting S_PoleCaptureTime				4			as _("Pole's capture time")
#Setting S_OffZoneAutoStartTime	90		as _("Time before auto activation of the tornado")
#Setting S_OffZoneShrinkDuration	45		as _("Tornado shrink duration")
#Setting S_EndRoundTimeLimit			60		as _("Time limit after the tornado is completly shrunk")
#Setting S_SpawnInterval					5			as _("Time between each wave of spawns")
#Setting S_OffZoneMinRadius			16.		as "<hidden>" ///< Minimum size of the OffZone at the end
// Matchmaking
#Setting S_NbPlayersMax	24	as "<hidden>"
#Setting S_NbPlayersMin	2		as "<hidden>"
#Setting S_MapsPerMatch	5		as "<hidden>"

#Setting S_ScriptEnvironment "production"/*/"development"*/

// ---------------------------------- //
// Constants
// ---------------------------------- //
#Const C_HudModulePath "Nadeo/ShootMania/Royal/Hud.Module.Gbx"
#Const C_ManiaAppUrl "file://Media/ManiaApps/Nadeo/ShootMania/Royal/RoyalFun.Script.txt"
#Const C_BotsNb 0
// Default player attribute
#Const C_Default_ArmorMaxOnRespawn 100 ///< Number of armors when respawning before the tornado
// Gameplay settings
#Const C_OffZoneStartRadius	300.
#Const C_DamageMax 100 ///< Maximum damage that can be inflected to a player in one hit
#Const C_EndRoundCelebrationTime 2500 ///< Time left to the winning player before the end of the round
#Const C_GaugeMultiplier 100 ///< Increase capture time precision
#Const C_BonusRespawnTime 10000 ///< Time before respawning a bonus
#Const C_RandomBonusSpawnInterval 8000 ///< Time between random bonus spawn
#Const C_MaxRandomBonuses 20 ///< Maxium number of random bonuses spawned at the same time

//L16N [Royal] Rules of the exp mode.
#Const Description _("Survive as long as possible, eliminate others players and capture the pole to score a maximum of points. Once eliminated you won't respawn before the next round. The first player to reach the points limit wins.")

// ---------------------------------- //
// Extends
// ---------------------------------- //
***MM_SetupMatchmaking***
***
declare Format = Integer[];
for (I, 1, S_NbPlayersMax) {
	Format.add(1);
}
MM_SetFormat(Format);
Format.clear();
declare ProgressiveFormat = Integer[][];
for (I, 1, S_NbPlayersMin) {
	Format.add(1);
}
ProgressiveFormat.add(Format);
for (I, S_NbPlayersMin+1, S_NbPlayersMax) {
	Format.add(1);
	ProgressiveFormat.add(Format);
}
MM_SetProgressiveFormats(ProgressiveFormat);
***

***Lobby_MatchRulesManialink***
***
ManialinkRules = """<label posn="-62.5 25" sizen="125 50" autonewline="1" maxline="10" textemboss="1" textsize="2" text="{{{Description}}}" />""";
***

***Match_LogVersions***
***
Log::RegisterScript(ScriptName, Version);
Log::RegisterScript(FunMgr::GetScriptName(), FunMgr::GetScriptVersion());
***

***Match_LoadLibraries***
***
FunMgr::Load();
***

***Match_UnloadLibraries***
***
FunMgr::Unload();
***

***Match_Yield***
***
FunMgr::Yield();
***

***Match_Settings***
***
MB_Settings_UseDefaultHud = False;
MB_Settings_UseDefaultTimer = False;
MB_Settings_UseDefaultUIManagement = False;
***

***Match_Rules***
***
ModeInfo::SetName("Royal Fun");
ModeInfo::SetType(ModeInfo::Type_FreeForAll());
ModeInfo::SetRules(Description);
ModeInfo::SetStatusMessage(GetStatusMessage(S_MapPointsLimit));
***

***Match_LoadHud***
***
Hud_Load(C_HudModulePath);
if (Hud != Null && Hud.ScoresTable != Null) {
	Hud.ScoresTable.Scores_Sort(CModulePlaygroundScoresTable::EScoreSortOrder::SMPoints);
}
***

***Match_SpawnScreen***
***
SpawnScreen::CreateScores("Score.RoundPoints + Score.Points");
***

***Match_StartServer***
***
// Initialize mode
UseClans = False;
ClientManiaAppUrl = C_ManiaAppUrl;
ChannelProgression::Enable(S_IsChannelServer);

// Objects::SetAllowedObjects([FunMgr::C_BoxItem]);
// Objects::SetAnchorsObjectsRespawnTime(C_BonusRespawnTime);
// Objects::SetAutoPickUp(False);

// Dispenser::SetDefaultObject([]);
// Dispenser::SetDefaultEmptyObject([]);

Dispenser::SetAutoPickUp(False);
Dispenser::SetAnchorsObjectsRespawnTime(C_BonusRespawnTime);

// Initialize UI
UIManager.UIAll.NoticesFilter_LevelToShowAsBigMessage = CUIConfig::ENoticeLevel::MatchInfo;
UIManager.UIAll.NoticesFilter_HideMapWarning = True; //< Hide pole capture notice
UICommon::SetMarkersVisibility(Teams[0], False);
UICommon::SetRoundInfoVisibility(Teams[0], False);
UICommon::SetSideScoresTableVisibility(Teams[0], False);
UICommon::SetRoundInfoModules(Teams[0], [
	UICommon::RoundInfoModule_Remaining(),
	UICommon::RoundInfoModule_Eliminator()
]);
if (Hud != Null && Hud.ScoresTable != Null) {
	Hud.ScoresTable.SetColumnVisibility("Team", False);
}
FunMgr::SetUIVisibility(False);

// Load actions
ActionList_Begin();
FunMgr::LoadActions();
ActionList_End();

// Load items
ItemList_Begin();
FunMgr::LoadItems();
Dispenser::LoadItems();
ItemList_End();
***

***Match_InitMap***
***
declare CSmMapLandmark Pole;
declare CSmMapLandmark[][Integer] Spawns; //< Spawn[][Priority]
***

***Match_StartMap***
***
// Setup fake players
Users_SetNbFakeUsers(C_BotsNb, 0);

// Find map pole
Pole <=> Map::GetLandmarkCapturable("Goal", 0);
if (Pole == Null) MB_StopMap();
CreateMarkers(Pole);

// Find map spawns
foreach (Spawn in MapLandmarks_PlayerSpawn) {
	if (!Spawns.existskey(Spawn.Order)) Spawns[Spawn.Order] = CSmMapLandmark[];
	Spawns[Spawn.Order].add(MapLandmarks_PlayerSpawn[Spawn.Id]);
}
Spawns = Spawns.sortkey();

// Initialize map scores
foreach (Score in Scores) {
	State::SetScore(Score, State::Points_Map(), 0);
	
	declare Royal_MapHitNb for Score = 0; ///< Number of hit done this map
	declare Royal_MapSurvivalNb for Score = 0; ///< Number of round survived this map
	declare Royal_MapPoleNb for Score = 0; ///< Number of pole capture this map
	Royal_MapHitNb = 0;
	Royal_MapSurvivalNb = 0;
	Royal_MapPoleNb = 0;
}
***

***Match_InitRound***
***
declare Integer NextSpawnWave;
declare Boolean CanStartCountdown;
declare Boolean CanSpawnPlayers;
declare Boolean CanCapturePole;
declare Integer OffZoneActivationTime;
declare CUser Capturer;
declare CSmMapLandmark[] RandomSpawns;
declare Integer StartingSurvivorsNb;
declare Integer PrevMarkersUpdate;
declare Boolean CanSpawnRandomBonuses;
declare Integer NextRandomBonusSpawn;
declare Integer SpawnOrderMax;
***

***Match_StartRound***
***
// Initialize pole
if (Pole.Base != Null) {
	Pole.Base.Clan = 0;
}
if (Pole.Gauge != Null) {
	Pole.Gauge.Clan = 0;
	Pole.Gauge.Value = 0;
	Pole.Gauge.Speed = 0;
	Pole.Gauge.Max = S_PoleCaptureTime * 1000 * C_GaugeMultiplier;
	if (Pole.Gauge.Max <= 0) Pole.Gauge.Max = 10;
}
Capturer = Null;

// Initialize offzone
OffZoneRadius = -1.;
OffZoneRadiusSpeed = 0.;
OffZoneCenterLandmarkId = Pole.Id;

// Let the server sleep until there is at least one player
WaitForPlayers();

// Initialize round scores
foreach (Score in Scores) {
	SetEliminator(Score, "");
	State::SetScore(Score, State::Points_Round(), 0);
	
	declare Royal_IsRespawn for Score = False;
	Royal_IsRespawn = False;
	
	declare Royal_HitNb for Score = 0;
	Royal_HitNb = 0;
	
	if (Hud != Null && Hud.ScoresTable != Null) {
		Hud.ScoresTable.ResetCustomColumns(Score);
	}
}

// Initialize fun mode
FunMgr::EnableBonus(FunMgr::C_Bonus_InstantCapture);
FunMgr::EnableBonus(FunMgr::C_Bonus_ArmorBonus);
FunMgr::EnableBonus(FunMgr::C_Bonus_AmmoBonus);

// Initialize UI
ModeInfo::SetStatusMessage(GetStatusMessage(S_MapPointsLimit));
UICommon::SetMarkersVisibility(Teams[0], True);
UICommon::SetRoundInfoVisibility(Teams[0], True);
FunMgr::SetUIVisibility(True);
if (Hud != Null && Hud.ScoresTable != Null) {
	Hud.ScoresTable.SetFooterText(TL::Compose("%1 "^S_MapPointsLimit, _("Points limit : ")));
}
//L16N [Royal] Message displayed while spawning players
UIManager.UIAll.StatusMessage = _("Spawning players");
UIManager.UIAll.MarkersXML = "";

// Initialize timers
StartTime = Now + 3000;
EndTime = -1;
OffZoneActivationTime = -1;
UIManager.UIAll.CountdownEndTime = OffZoneActivationTime;

MB_EnablePlayMode(True);
ClearSurvivors();
NextSpawnWave = Now;
CanStartCountdown = True;
CanSpawnPlayers = True;
CanCapturePole = False;
RandomSpawns.clear();
StartingSurvivorsNb = 0;
PrevMarkersUpdate = -1;
CanSpawnRandomBonuses = False;
NextRandomBonusSpawn = Now;
SpawnOrderMax = -1;
***

***Match_PlayLoop***
***

// Manage events
foreach (Event in PendingEvents) {
	// Discard further events if the round must end
	if (!MB_RoundIsRunning()) {
		Events::Invalid(Event);
	}
	// On hit
	else if (Event.Type == CSmModeEvent::EType::OnHit) {
		if (Event.Victim == Null || Event.Shooter == Null) { //< Hit shield or turret shot
			Event.Damage = C_DamageMax;
			Events::Valid(Event);
		} else if (Event.Shooter == Event.Victim) { //< Hit self
			Events::Invalid(Event);
		} else {
			// Ceiling the damages at 100
			Event.Damage = C_DamageMax;
			
			declare Points = 0;
			
			if (Event.Shooter != Null && Event.Shooter.Score != Null) {
				declare Royal_HitNb for Event.Shooter.Score = 0;
				Points += (Royal_HitNb / 2) + 1;
				Royal_HitNb += 1;
			}
			
			Event.ShooterPoints = Points;
			State::UpdateScore(Event.Shooter.Score, State::Points_RoundHit(), Points);
			
			Events::Valid(Event);
		}
	}
	// On armor empty
	else if (Event.Type == CSmModeEvent::EType::OnArmorEmpty) {
		if (!CanSpawnPlayers) {
			// Remove player from the survivors list
			declare CSmScore Eliminated;
			declare CSmScore Eliminator;
			if (Event.Victim != Null) Eliminated <=> Event.Victim.Score;
			if (Event.Shooter != Null) Eliminator <=> Event.Shooter.Score;
			RemoveSurvivor(Eliminated, Eliminator, True);
			
			if (Event.Victim != Null && Event.Shooter != Null) {
				SetEliminator(Event.Victim.Score, Event.Shooter.User.Name);
			}
			
			if (Event.Victim != Null && Event.Victim.Score != Null) {
				declare Royal_SpawnOrder for Event.Victim.Score = Now;
				Royal_SpawnOrder = Now;
			}
		}
		Events::Valid(Event);
	}
	// On player requests respawn
	else if (Event.Type == CSmModeEvent::EType::OnPlayerRequestRespawn) {
		if (!CanSpawnPlayers) {
			// Remove player from the survivors list
			declare CSmScore Eliminated;
			if (Event.Player != Null) Eliminated <=> Event.Player.Score;
			RemoveSurvivor(Eliminated, Null, True);
			
			if (Event.Player != Null && Event.Player.Score != Null) {
				declare Royal_SpawnOrder for Event.Player.Score = Now;
				Royal_SpawnOrder = Now;
			}
		}
		Events::Valid(Event);
	}
	// On pole capture
	else if (Event.Type == CSmModeEvent::EType::OnCapture) {
		if (Event.Landmark == Pole) {
			OffZoneActivationTime = Now;
			if (Pole.Base != Null) Pole.Base.Clan = Pole.Gauge.Clan;
			
			// Cancel "Instant Capture" bonus
			foreach (Player in Players) {
				if (FunMgr::HasTimedBonus(Player, FunMgr::C_Bonus_InstantCapture)) {
					FunMgr::RemoveTimedBonus(Player, FunMgr::C_Bonus_InstantCapture);
				}
			}
	
			// Share pole points between players on the sector
			if (Event.Landmark.Sector != Null && Event.Landmark.Sector.PlayersIds.count > 0) {
				declare CapturingPlayers = CSmPlayer[];
				foreach (PlayerId in Event.Landmark.Sector.PlayersIds) {
					if (Players.existskey(PlayerId)) {
						declare Player <=> Players[PlayerId];
						
						if (!CapturingPlayers.exists(Player)) {
							CapturingPlayers.add(Player);
						}
						
						// Send a notice for each capturing player
						UIManager.UIAll.SendNotice(
							TL::Compose(_("$<%1$> captured the pole."), Player.User.Name), 
							CUIConfig::ENoticeLevel::MatchInfo, Null, 
							CUIConfig::EAvatarVariant::Default, 
							CUIConfig::EUISound::Capture, 0
						);
					}
				}
				if (CapturingPlayers.count > 0) {
					declare Points = PlayersNbAlive / CapturingPlayers.count;
				
					foreach (Player in CapturingPlayers) {
						State::UpdateScore(Player.Score, State::Points_RoundPole(), Points);
					}
				}
			}
		}
		Events::Valid(Event);
	}
	// On players touches object
	else if (Event.Type == CSmModeEvent::EType::OnPlayerTouchesObject) {
		// A player can not pick up two bonuses at the same time
		if (FunMgr::IsBonus(Event.Object) && FunMgr::HasTimedBonus(Event.Player)) {
			declare RoyalFun_LastBonusWarnSound for Event.Player = 0;
			if (Now - RoyalFun_LastBonusWarnSound > 500) {
				RoyalFun_LastBonusWarnSound = Now;
				FunMgr::CantTakeBonus(Event.Player);
			}
			Events::Invalid(Event);
		} else if(Dispenser::IsObjectEmptyBonus(Event.Object)) {
			// declare RoyalFun_EmptyBonusWarnSound for Event.Player = Now;
			// if (Now - RoyalFun_EmptyBonusWarnSound > 1000) {
				// RoyalFun_EmptyBonusWarnSound = Now;
				// FunMgr::CantTakeBonus(Event.Player);
			// }
			Events::Invalid(Event);
		} else {
			if(Dispenser::IsDispanserObject(Event.Object)) {
				Dispenser::PickUp(Event.Object);
				// declare RoyalFun_EmptyBonusWarnSound for Event.Player = Now;
				// RoyalFun_EmptyBonusWarnSound = Now;
			} else {
				Objects::PickUp(Event.Object);
			}
			Events::Valid(Event);
		}
	}
	// Others
	else {
		Events::Valid(Event);
	}
	
	if (Events::Validated(Event)) {
		FunMgr::OnEvent(Event);
	}
}

// Spawn players
if (CanSpawnPlayers && NextSpawnWave > 0 && Now >= NextSpawnWave) {
	// List players waiting to spawn
	declare Integer[CSmPlayer] WaitingToSpawn;
	foreach (Player in Players) {
		if (Player.SpawnStatus == CSmPlayer::ESpawnStatus::NotSpawned && Player.Score != Null && MM_PlayerIsAllowedToPlay(Player)) {
			declare Royal_IsRespawn for Player.Score = False;
			if (!Royal_IsRespawn) {
				declare Royal_SpawnOrder for Player.Score = Now;
				WaitingToSpawn[Player] = Royal_SpawnOrder;
			}
		}
	}
	WaitingToSpawn = WaitingToSpawn.sort();
	
	// Spawn players
	if (WaitingToSpawn.count > 0) {
		foreach (Priotity => SpawnsList in Spawns) {
			// Randomize spawns list
			declare RandomSpawnsList = SpawnsList;
			while (RandomSpawnsList.count > 0) {
				declare Spawn <=> RandomSpawnsList[ML::Rand(0, RandomSpawnsList.count-1)];
				declare Removed = RandomSpawnsList.remove(Spawn);
				
				// Get player to spawn
				declare CSmPlayer Player;
				foreach (PlayerToSpawn => SpawnOrder in WaitingToSpawn) {
					Player <=> PlayerToSpawn;
					break;
				}
				Removed = WaitingToSpawn.removekey(Player);
				
				if (Player != Null) {
					// Setup player
					FunMgr::InitializePlayer(Player);
					
					// Spawn player
					SM::Spawn(Player, 0, Player.ArmorMax, Spawn.PlayerSpawn, Now + 3000);
					declare Royal_IsRespawn for Player.Score = False;
					Royal_IsRespawn = True;
					SpawnOrderMax = Priotity;
				}
				
				if (WaitingToSpawn.count <= 0) break;
			}
			if (WaitingToSpawn.count <= 0) break;
		}
	}
	
	// Setup next spawn wave if necessary
	if (WaitingToSpawn.count > 0) {
		declare SpawnInterval = S_SpawnInterval * 1000;
		if (SpawnInterval < 0) SpawnInterval = 0;
		NextSpawnWave = Now + SpawnInterval;
	} else {
		NextSpawnWave = -1;
		CanSpawnPlayers = True;
		UIManager.UIAll.StatusMessage = "";
	}
}

// Start offzone activation countdown and round time limit
if (CanStartCountdown && NextSpawnWave < 0) {
	CanStartCountdown = False;
	CanCapturePole = True;
	
	// Offzone activation countdown
	declare OffZoneAutoStartTime = S_OffZoneAutoStartTime * 1000;
	if (OffZoneAutoStartTime < 0) OffZoneAutoStartTime = 0;
	OffZoneActivationTime = Now + 3000 + OffZoneAutoStartTime;
	
	UIManager.UIAll.CountdownEndTime = OffZoneActivationTime;
	
	// End round countdown
	if (S_EndRoundTimeLimit >= 0) {
		declare OffZoneShrinkDuration = S_OffZoneShrinkDuration * 1000;
		if (OffZoneShrinkDuration < 0) OffZoneShrinkDuration = 10;
		EndTime = UIManager.UIAll.CountdownEndTime + OffZoneShrinkDuration + (S_EndRoundTimeLimit * 1000);
	}
}

// Respawn players
if (CanSpawnPlayers && NextSpawnWave < 0) {
	foreach (Player in Players) {
		if (Player.SpawnStatus == CSmPlayer::ESpawnStatus::NotSpawned && MM_PlayerIsAllowedToPlay(Player)) {
			// Select a spawn
			if (RandomSpawns.count <= 0) {
				declare OldestPlayerSpawn <=> CSmMapLandmark;
				declare OldestSpawnTime = -1;
				// Find spawns that have not been used for at least 10s
				foreach (Priority => SpawnsList in Spawns) {
					foreach (Spawn in SpawnsList) {
						declare Royal_LastSpawnTime for Spawn = -1;
						if (Royal_LastSpawnTime < 0 || Now - Royal_LastSpawnTime > 10000) {
							if (SpawnOrderMax <= 0 || Spawn.Order <= SpawnOrderMax) { //< Do not spawn in disabled spawners
								RandomSpawns.add(Spawn);
							}
						}
						if (OldestSpawnTime < 0 || Royal_LastSpawnTime < OldestSpawnTime) {
							if (SpawnOrderMax <= 0 || Spawn.Order <= SpawnOrderMax) { //< Do not spawn in disabled spawners
								OldestSpawnTime = Royal_LastSpawnTime;
								OldestPlayerSpawn <=> Spawn;
							}
						}
						Log::Log("""> Spawn {{{Spawn.Id}}} > SpawnOrderMax : {{{SpawnOrderMax}}} | MapLandmark.Order : {{{Spawn.Order}}} | Royal_LastSpawnTime : {{{Royal_LastSpawnTime}}}""");
					}
				}
				// If all spawns were used recently, use the oldest one
				if (RandomSpawns.count <= 0) {
					RandomSpawns.add(OldestPlayerSpawn);
				}
			}
			
			Log::Log("""> RandomSpawns : {{{RandomSpawns}}}""");
			
			if (RandomSpawns.count > 0) {
				// Setup player
				FunMgr::InitializePlayer(Player);
				
				// Player always respawn with 1 armor
				Player.ArmorMax = C_Default_ArmorMaxOnRespawn;
			
				declare Spawn <=> RandomSpawns[ML::Rand(0, RandomSpawns.count-1)];
				declare Royal_LastSpawnTime for Spawn = -1;
				Royal_LastSpawnTime = Now;
			
				SM::Spawn(Player, 0, Player.ArmorMax, Spawn.PlayerSpawn, Now + 3000);
				declare Removed = RandomSpawns.remove(Spawn);
			}
		}
	}
}

// Update UI
UICommon::SetRemainingPlayersNb(Teams[0], PlayersNbAlive);
if (PrevMarkersUpdate != FunMgr::GetMarkersUpdate()) {
	PrevMarkersUpdate = FunMgr::GetMarkersUpdate();
	UIManager.UIAll.MarkersXML = FunMgr::GetMarkers();
}

// Pole capture
if (CanCapturePole) {
	if (Pole.Sector.PlayersIds.count > 0) {
		declare Player <=> AllPlayers[Pole.Sector.PlayersIds[0]];
		declare CaptureSpeed = 1;
		if (FunMgr::HasTimedBonus(Player, FunMgr::C_Bonus_InstantCapture)) {
			CaptureSpeed = Pole.Gauge.Max;
		}
		Pole.Gauge.Speed = CaptureSpeed * C_GaugeMultiplier;
		
		if (Capturer == Null) {
			declare Player <=> AllPlayers[Pole.Sector.PlayersIds[0]];
			Capturer <=> Player.User;
			Teams[0].ColorPrimary = Player.User.Color;
			Pole.Gauge.Clan = 1;
		}
	} else {
		Pole.Gauge.Speed = 0;
		if (Capturer != Null) {
			Capturer = Null;
			Pole.Gauge.Clan = 0;
		}
	}
}

// Tornado activation
if (OffZoneActivationTime >= 0 && Now >= OffZoneActivationTime) {
	OffZoneActivationTime = -1;
	UIManager.UIAll.CountdownEndTime = -1;
	
	CanSpawnPlayers = False;
	CanCapturePole = False;
	Pole.Gauge.Speed = 0;
	
	// Fill survivors list
	declare CSmPlayer[] Survivors;
	foreach (Player in Players) {
		if (Player.SpawnStatus != CSmPlayer::ESpawnStatus::NotSpawned) {
			Survivors.add(Player);
		}
	}
	StartingSurvivorsNb = FillSurvivors(Survivors);
	
	// Setup tornado
	declare OffZoneShrinkDuration = S_OffZoneShrinkDuration * 1000;
	if (OffZoneShrinkDuration <= 0) OffZoneShrinkDuration = 10;
	OffZoneRadius = C_OffZoneStartRadius;
	OffZoneRadiusSpeed = (OffZoneRadius - S_OffZoneMinRadius) / (OffZoneShrinkDuration / 1000);
	
	// Start end round timer
	if (S_EndRoundTimeLimit >= 0) {
		EndTime = Now + OffZoneShrinkDuration + (S_EndRoundTimeLimit * 1000);
	}
	
	// Use notice to display big message after the capturers name
	UIManager.UIAll.SendNotice(
		_("Tornado activated."), 
		CUIConfig::ENoticeLevel::MatchInfo, Null, 
		CUIConfig::EAvatarVariant::Default, 
		CUIConfig::EUISound::PhaseChange, 0
	);
	
	// Disable pole capture bonus
	FunMgr::DisableBonus(FunMgr::C_Bonus_InstantCapture);
}

// Tornado stop
if (OffZoneRadiusSpeed > 0. && OffZoneRadius >= 0. && OffZoneRadius <= S_OffZoneMinRadius) {
	OffZoneRadiusSpeed = 0.;
	OffZoneRadius = S_OffZoneMinRadius;
	
	// Enforce end round countdown
	if (S_EndRoundTimeLimit >= 0) {
		EndTime = Now + (S_EndRoundTimeLimit * 1000);
	}
	
	// Allow only SuperShootman, SuperAmmoGain and Shield during the final battle
	FunMgr::DisableBonus(FunMgr::C_Bonus_ArmorBonus);
	FunMgr::DisableBonus(FunMgr::C_Bonus_AmmoBonus);
			
	CanSpawnRandomBonuses = True;
	NextRandomBonusSpawn = Now + C_RandomBonusSpawnInterval;
}

// Spawn random bonus around the pole
if (CanSpawnRandomBonuses) {
	if (Now >= NextRandomBonusSpawn) {
		NextRandomBonusSpawn = Now + C_RandomBonusSpawnInterval;
		if (FunMgr::CountBonuses() < C_MaxRandomBonuses) {
			declare Bonus <=> FunMgr::CreateBonus();
			if (Bonus != Null) {
				declare Angle = ML::Rand(-ML::PI(), ML::PI());
				declare Direction = <ML::Cos(Angle), ML::Sin(Angle)>;
				declare Position = Pole.Position + <2. * Direction.X, 16., 2. * Direction.Y>;
				declare Velocity = <Direction.X, -1., Direction.Y> * ML::Rand(1., 5.);
				Bonus.SetPositionAndVel(Position, Velocity);
			}
		}
	}
}

// Remove disconnected players and spectators
CleanSurvivors();

// End round conditions
if (MB_RoundIsRunning() && EndTime > 0 && Now >= EndTime) {
	MB_StopRound();
}
if (
	MB_RoundIsRunning() && 
	(PlayersNbAlive <= 0 || (StartingSurvivorsNb >= 2 && PlayersNbAlive <= 1))
) {
	MB_StopRound();
}
***

***Match_EndRound***
***
Message::CleanAllMessages();
UIManager.UIAll.StatusMessage = "";
UICommon::SetMarkersVisibility(Teams[0], False);
UICommon::SetRoundInfoVisibility(Teams[0], False);
FunMgr::SetUIVisibility(False);
FunMgr::DestroyBonuses();

// Stop tornado, pole and timer
OffZoneRadiusSpeed = 0.;
OffZoneRadius = -1.;
Pole.Gauge.Speed = 0;
UIManager.UIAll.CountdownEndTime = -1;
EndTime = -1;

// Find survivors for channel progression
foreach (Score in Scores) {
	declare Royal_RoundSurvivor for Score = False;
	Royal_RoundSurvivor = False;
}
foreach (Player in Players) {
	if (Player.SpawnStatus == CSmPlayer::ESpawnStatus::Spawned && Player.Armor > 0) {
		declare Royal_RoundSurvivor for Player.Score = False;
		Royal_RoundSurvivor = True;
		Log::Log("""[Royal] {{{Player.User.Login}}} survived the round""");
	}
}

// Find the round winner and give the last survival points if
// there is only one survivor. Otherwise the round is a draw.
foreach (Player in Players) {
	if (Player.SpawnStatus != CSmPlayer::ESpawnStatus::NotSpawned) {
		RemoveSurvivor(Player.Score, Null, PlayersNbAlive <= 1);
		if (PlayersNbAlive <= 1) {
			Scores::SetPlayerWinner(Player.Score);
		}
		if (Player.Score != Null) {
			declare Royal_SpawnOrder for Player.Score = Now;
			Royal_SpawnOrder = Now;
		}
	}
}

foreach (Score in Scores) {
	declare RoundHit = State::GetScore(Score, State::Points_RoundHit());
	declare RoundSurvival = State::GetScore(Score, State::Points_RoundSurvival());
	declare RoundPole = State::GetScore(Score, State::Points_RoundPole());
	
	// Update map scores
	State::UpdateScore(Score, State::Points_MapHit(), RoundHit);
	State::UpdateScore(Score, State::Points_MapSurvival(), RoundSurvival);
	State::UpdateScore(Score, State::Points_MapPole(), RoundPole);
	
	// Update channel scores
	declare Royal_MapHitNb for Score = 0;
	declare Royal_MapSurvivalNb for Score = 0;
	declare Royal_MapPoleNb for Score = 0;
	declare Royal_RoundSurvivor for Score = False;
	declare Royal_HitNb for Score = 0;
	Royal_MapHitNb += Royal_HitNb;
	if (Royal_RoundSurvivor) Royal_MapSurvivalNb += 1;
	if (RoundPole > 0) Royal_MapPoleNb += 1;
}

// Display winner name
DisplayWinnerName(Scores::GetPlayerWinner(), C_EndRoundCelebrationTime);

// Cleanup
declare MinSpawnOrder = StartTime;
StartTime = -1;
SM::UnspawnAllPlayers();
MB_EnablePlayMode(False);

// End round sequence
EndRoundSequence(7500);

// Update scores
Scores::SetPlayerWinner(Null);
Scores::AffectPlayersRoundToMapPoints();
declare MaxPoints = -1;
foreach (Score in Scores) {
	// Update spawn order
	declare Royal_SpawnOrder for Score = Now;
	if (Royal_SpawnOrder <= MinSpawnOrder) {
		Royal_SpawnOrder = Now;
	}
	
	// End match conditions
	declare Points = Scores::GetPlayerMapPoints(Score);
	if (Points >= S_MapPointsLimit && Points >= MaxPoints) {
		if (Points > MaxPoints) {
			Scores::SetPlayerWinner(Score);
		} else if (Points == MaxPoints) {
			Scores::SetPlayerWinner(Null);
		}
		MaxPoints = Points;
	}
}

declare MapWinner <=> Scores::GetPlayerWinner();
if (MapWinner != Null) {
	MB_StopMap();
}
***

***Match_EndMap***
***
if (MM_IsMatchServer()) {
	if (MB_GetMapCount() >= S_MapsPerMatch) MB_StopMatch();
} else {
	MB_StopMatch();
}

DestroyMarkers();

// Display map scores
foreach (Score in Scores) {
	State::SetScore(Score, State::Points_RoundHit(), State::GetScore(Score, State::Points_MapHit()));
	State::SetScore(Score, State::Points_RoundSurvival(), State::GetScore(Score, State::Points_MapSurvival()));
	State::SetScore(Score, State::Points_RoundPole(), State::GetScore(Score, State::Points_MapPole()));
	Scores::SetPlayerRoundPoints(Score, 0);
}

Scores::SetPlayerWinner(Scores::GetBestPlayerMapPoints(Scores::Order_Descending()));
***

***Match_BeforeCloseLadder***
***
if (ChannelProgression::IsEnabled()) {
	declare RoundCount = MB_GetRoundCount();
	declare ReferenceHitNb = 6 * RoundCount;
	foreach (Score in Scores) {
		declare Royal_MapHitNb for Score = 0;
		if (Royal_MapHitNb > ReferenceHitNb) ReferenceHitNb = Royal_MapHitNb;
	}
	foreach (Score in Scores) {
		declare Royal_MapHitNb for Score = 0;
		declare Royal_MapSurvivalNb for Score = 0;
		declare Royal_MapPoleNb for Score = 0;
		
		declare SurvivalPerf = 0.;
		declare PolePerf = 0.;
		declare HitPerf = 0.;
		if (RoundCount != 0) {
			SurvivalPerf = ((Royal_MapSurvivalNb / (RoundCount * 1.)) * 0.5);
			PolePerf = ((Royal_MapPoleNb / (RoundCount * 1.)) * 0.1);
		}
		if (ReferenceHitNb != 0) {
			HitPerf = ((Royal_MapHitNb / (ReferenceHitNb * 1.)) * 0.4);
		}
		declare Performance = SurvivalPerf + PolePerf + HitPerf;
		
		Log::Log("""[TimeAttack] Performance > {{{Score.User.Login}}} > RoundCount : {{{RoundCount}}} | Royal_MapSurvivalNb : {{{Royal_MapSurvivalNb}}} | Royal_MapPoleNb : {{{Royal_MapPoleNb}}} | Royal_MapHitNb : {{{Royal_MapHitNb}}} | ReferenceHitNb : {{{ReferenceHitNb}}} | SurvivorPerf : {{{SurvivalPerf}}} | PolePerf : {{{PolePerf}}} | HitPerf : {{{HitPerf}}} | Performance: {{{Performance}}}""");
		
		ChannelProgression::SetPlayerPerformance(Score, Performance);
	}
}
***
