/**
 * 
 */
#Const Version    "2018-09-03"
#Const ScriptName "ManiaApps/RoyalPlayerUI.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Libraries
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Include "TextLib" as TL
#Include "MathLib" as ML
#Include "ManiaApps/Nadeo/Layers.Script.txt" as Layers
#Include "Libs/Nadeo/Shootmania/Royal/Constants2.Script.txt" as Const

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Constants
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Const C_Layer_SquadRoyalTutoUI "RoyalSquadTUTO_UI"

#Const C_PathRoyal "file://Media/Manialinks/Nadeo/ShootMania/Royal/"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Private
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Get the manialink of the Royal UI
 *
 *  @return                           Royal UI manialink
 */

Text RoyalSquadTUTO_ML(){

declare Img_Background = C_PathRoyal^"Fond_Tuto.png";
declare Img_Hit = C_PathRoyal^"ASSET_UI_SquadRoyal_HitIcon.dds";
declare Img_Pole = C_PathRoyal^"ASSET_UI_SquadRoyal_Pole.dds";
declare Img_Survie = C_PathRoyal^"ASSET_UI_SquadRoyal_PersonnePointille.dds";
declare Img_CadreValidate = "file://Media/ManiaLinks/Nadeo/TeamSelectionModule/ASSETS_512x512_SquadRoyal_Tuto_CadreValidate.dds";
declare Img_CadreTuto_Score = "file://Media/ManiaLinks/Nadeo/TeamSelectionModule/ASSETS_512x512_SquadRoyal_Tuto_CadreScore.dds";

//Title HEAL
//L16N [Royal] Used as a header to explain the heal mecanics.
declare TutoRoyalSquad_HEAL = _("HEAL");
//L16N [Royal] The complete message means that the players can shoot their team mates to heal them.
declare TutoRoyalSquad_ShootToHeal_Text = _("$<$8EFShoot$> $<$FFFa teammate to$> $<$8EFheal$> $<$FFFthem$>");
//L16N [Royal] Used as a header to explain the Score mecanics.
declare TutoRoyalSquad_Score = _("SCORE");
//L16N [Royal] Used to tell the players that he can capture the pole
declare TutoRoyalSquad_Capture_Text = _("|Capture the Pole : part 1 of 2|Capture");
//L16N [Royal] Used to tell the players that he can capture the pole
declare TutoRoyalSquad_ThePole_Text = _("|Capture the Pole : part 2 of 2|The Pole");
//L16N [Royal] Used to tell the players that he can hits opponents
declare TutoRoyalSquad_Hit_Text = _("|Hit opponents : part 1 of 2|Hit");
//L16N [Royal] Used to tell the players that he can hits opponents
declare TutoRoyalSquad_Opponent_Text = _("|Hit opponents : part 2 of 2|opponents");
//L16N [Royal] Used to tell the players that he have to survive the round
declare TutoRoyalSquad_Survive_Text = _("|Survive the round : part 1 of 2|Survive");
//L16N [Royal] Used to tell the players that he have to survive the round
declare TutoRoyalSquad_TheRound_Text = _("|Survive the round : part 2 of 2|the round");
//L16N [Royal] Used to tell the players that he validate the tutorial by clicking on the button
declare TutoRoyalSquad_Validate_Text = _("Validate");
    return """	
		<manialink version="3" name="{{{C_Layer_SquadRoyalTutoUI}}}">
<frame id="frame-global" hidden="1" z-index="100">
	<quad z-index="-148" size="330 181" valign="center" bgcolor="000000FF" opacity="0.6" id="blurscreen" pos="-161 -0.266"/>
	<quad z-index="-149" size="330 181" valign="center" bgcolor="6F6F6FFF" substyle="BgDialogBlur" bluramount="0.95" opacity="0.3" id="blurscreen" pos="-161 -0.266"/>
	<label pos="-150 80" z-index="0" size="53 14" text="Royal Squad" textsize="12" textfont="OswaldMono" textcolor="FFFFFFFF"/>
	<frame pos="10 0">
		<frame scale="1.2" pos="17.5 -14">
			<label pos="-90 37" z-index="0" size="120 14" text="{{{TutoRoyalSquad_ShootToHeal_Text}}}" textsize="12" textfont="OswaldMono" textcolor="7CE5FFFF"/>
			<label pos="-90 56" z-index="0" size="28 14" text="{{{TutoRoyalSquad_HEAL}}}" textsize="15" textfont="OswaldMono" textemboss="1" textcolor="FFFFFFFF"/>
			<quad pos="27 41.5" z-index="0" size="15 .5" bgcolor="FFFA" halign="right"/>
			<quad pos="-92 41" z-index="0" size="119 0.5" bgcolor="FFFA"/>
			<quad pos="-92 55" z-index="0" size="1 12" bgcolor="FFFF"/>
		</frame>
		<frame scale="1.1" pos="8 4">
			<label pos="-85 -23" z-index="0" size="20.5 7" text="{{{TutoRoyalSquad_Capture_Text}}}" textsize="6" textfont="OswaldMono" textcolor="7CE5FFFF"/>
			<label pos="-18.3 -23" z-index="0" size="20.5 7" text="{{{TutoRoyalSquad_Hit_Text}}}" textsize="6" textfont="OswaldMono" textcolor="7CE5FFFF" halign="center"/>
			<label pos="34.5 -23" z-index="0" size="21.5 7" text="{{{TutoRoyalSquad_Survive_Text}}}" textsize="6" textfont="OswaldMono" textcolor="7CE5FFFF" halign="center"/>
			<label pos="34.5 -30" z-index="0" size="21.5 7" text="{{{TutoRoyalSquad_TheRound_Text}}}" textsize="4" textfont="OswaldMono" textcolor="FFFFFFFF" halign="center"/>
			<label pos="-82.5 -30.5" z-index="0" size="15.5 5" text="{{{TutoRoyalSquad_ThePole_Text}}}" textsize="4" textfont="OswaldMono" textcolor="FFFF"/>
			<label pos="-27 -30" z-index="0" size="18.5 5.97" text="{{{TutoRoyalSquad_Opponent_Text}}}" textsize="4" textfont="OswaldMono" textcolor="FFFF"/>
			<label pos="-90 0" z-index="0" size="36 14" text="{{{TutoRoyalSquad_Score}}}" textsize="15" textfont="OswaldMono" textemboss="1" textcolor="FFFFFFFF"/>
			<quad pos="-92 -1" z-index="0" size="1 12" bgcolor="FFFF"/>
			<quad pos="67 -15" size="30 .5" bgcolor="FFFA" halign="right"/>
			<quad pos="-92 -15.5" size="159 0.421" bgcolor="FFFA"/>
			<quad pos="-89 -21" z-index="0" size="41 36.5" bgcolor="FFFA" image="{{{Img_CadreTuto_Score}}}" id="imagecadre1"/>
			<quad pos="-67 -39" z-index="0" size="15 15" bgcolor="FFFA" image="{{{Img_Pole}}}" id="iconpole"/>
			<quad pos="-12 -39" z-index="0" size="15 15" bgcolor="FFFA" image="{{{Img_Hit}}}" id="iconhit"/>
			<quad pos="42.5 -39" z-index="0" size="15 15" bgcolor="FFFA" image="{{{Img_Survie}}}" id="iconsurvive"/>
			<quad pos="-34.5 -21" z-index="0" size="41 36.5" bgcolor="FFFA" image="{{{Img_CadreTuto_Score}}}" id="imagecadre2"/>
			<quad pos="20 -21" z-index="0" size="41 36.5" bgcolor="FFFA" image="{{{Img_CadreTuto_Score}}}" id="imagecadre3"/>
		</frame>
		<frame>
			<label pos="50 -69" z-index="3" size="15 5" text="{{{TutoRoyalSquad_Validate_Text}}}" textsize="4" textfont="OswaldMono"  textcolor="FFFFFFFF"/>
			<quad pos="57.5 -71.75" z-index="-5" size="26 10.5" bgcolor="1A1A1AFF" halign="center" valign="center" scriptevents="1" opacity=".5" id="ForefrontBackground_ValidateButton"/>
			<quad pos="44.5 -58.5" z-index="2" size="26 26" bgcolor="00000099" image="{{{Img_CadreValidate}}}" id="PourContour" opacity="1."/> 
		</frame>
	</frame>
	<quad pos="-87 56" z-index="-5" size="184 120" bgcolor="00000099"/>
	<!--<quad pos="-160 90" z-index="-20" size="320 180" bgcolor="FFFF" image="{{{Img_Background}}}" id="Background"/>-->
</frame>
<script><!--
#Include "TextLib" as TL
#Include "MathLib" as ML
CSmPlayer GetCSmPlayerFromLogin(Text _PlayerName){
	foreach(Player in Players){
		if (Player.User.Login == _PlayerName) {
			return Player;
		}
	}
	return Null;
}
main() {
	declare Boolean IsTutoFrameVisible for UI;
	declare persistent RoyalSquadTuto_Validated for LocalUser = False;
	declare G_Frame_Globale <=> (Page.GetFirstChild("frame-global") as CMlFrame);
	declare G_Quad_Validate <=> (Page.GetFirstChild("ForefrontBackground_ValidateButton") as CMlQuad);
	declare G_Quad_ValidateCadre <=> (Page.GetFirstChild("PourContour") as CMlQuad);
	declare Boolean DisplayTutorial;
	declare CSmPlayer PlayerOwner;
	
	declare Boolean IsOverValidate = False;
	declare Real G_X;
	IsTutoFrameVisible = False;
	while (True) {
		yield;
		PlayerOwner <=> GetCSmPlayerFromLogin(LocalUser.Login);
		if(!RoyalSquadTuto_Validated || DisplayTutorial){
			G_Frame_Globale.Visible = True;
			IsTutoFrameVisible = True;
		} else {
			IsTutoFrameVisible = False;
			G_Frame_Globale.Visible = False;
		} 

		if(PlayerOwner != Null){
			if(PlayerOwner.SpawnStatus != CSmPlayer::ESpawnStatus::NotSpawned){
				DisplayTutorial = False;
			}
		}
		
		if(IsOverValidate) {
			G_X = Now * 0.003;
			G_Quad_ValidateCadre.Opacity = ML::Abs(ML::Sin(G_X))/1.5 + 0.3;
		} else {
			G_X = 0.;
			G_Quad_ValidateCadre.Opacity = 1.;
		}
		
		foreach (Event in PendingEvents) {
			switch (Event.Type) {
				case CMlScriptEvent::Type::KeyPress: {
					if(PlayerOwner != Null){
						if(Event.KeyName == "H" && PlayerOwner.SpawnStatus == CSmPlayer::ESpawnStatus::NotSpawned){
							DisplayTutorial = True;
							IsTutoFrameVisible = True;
						}
					}
				}
				case CMlScriptEvent::Type::MouseClick: {
					if(Event.ControlId == "ForefrontBackground_ValidateButton"){
						declare persistent RoyalSquadTuto_Validated for LocalUser = False;
						RoyalSquadTuto_Validated = True;
						DisplayTutorial = False;
						IsTutoFrameVisible = False;
					}
				}
				
				case CMlScriptEvent::Type::MouseOver : {
					G_Quad_Validate.Opacity = 1.;
					IsOverValidate = True;
				}
				
				case CMlScriptEvent::Type::MouseOut : {
					G_Quad_Validate.Opacity = 0.5;
					IsOverValidate = False;
				}
			}
		}
		
	}
}
--></script>
</manialink>""";
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Public
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Update library
Void Yield() {

}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Unload the library
Void Unload() {

}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Load the library
Void Load() {
	Unload();
	Layers::Create(C_Layer_SquadRoyalTutoUI, RoyalSquadTUTO_ML());
}