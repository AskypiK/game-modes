/**
 *	Lint manialink
 */
#Const Version    "2016-11-16"
#Const ScriptName "Libs/Nadeo/Manialink/Modules/Lint.Script.txt"

// ---------------------------------- //
// Libraries
// ---------------------------------- //
#Include "Libs/Nadeo/Log.Script.txt" as Log
#Include "Libs/Nadeo/Manialink/ManiaView.Script.txt" as MV

// ---------------------------------- //
// Constants
// ---------------------------------- //
#Const C_LogPrefix "[ManiaView][Lint]"

// ---------------------------------- //
// Functions
// ---------------------------------- //
// ---------------------------------- //
// Public
// ---------------------------------- //
// ---------------------------------- //
/** Return the version number of the script
 *
 *  @return   The version number of the script
 */
Text GetScriptVersion() {
  return Version;
}

// ---------------------------------- //
/** Return the name of the script
 *
 *  @return   The name of the script
 */
Text GetScriptName() {
  return ScriptName;
}

// ---------------------------------- //
/** Run the module
 *
 *	@param	_Name											The name of the manialink
 *	@param	_Manialink								The manialink to process
 *
 *	@return														The processed manialink
 */
Text[][Integer] Run(Text _Name, Text[][Integer] _Manialink) {
	// Parse manialink and extract ManiaView attributes info
	if (_Manialink.existskey(MV::Slot_Manialink())) {
		declare Wrap = "<root>"^MV::CompileSlot(_Manialink[MV::Slot_Manialink()])^"</root>";
		declare XmlDoc = Xml.Create(Wrap, False, True, False);
		
		foreach (Node in XmlDoc.Nodes) {
			declare Attributes = MV::ExtractAttributes(Node, "", ["posn", "sizen"]);
			foreach (Attribute => Value in Attributes) {
				if (Attribute == "posn") {
					Log::Log("""{{{C_LogPrefix}}}[{{{_Name}}}] The "posn" attribute is not valid anymore. Use "pos" instead. -> "{{{Node.TextRawContents}}}" """);
				} else if (Attribute == "sizen") {
					Log::Log("""{{{C_LogPrefix}}}[{{{_Name}}}] The "sizen" attribute is not valid anymore. Use "size" instead. -> "{{{Node.TextRawContents}}}" """);
				}
			}
		}
		
		Xml.Destroy(XmlDoc);
	}
	
	return _Manialink;
}