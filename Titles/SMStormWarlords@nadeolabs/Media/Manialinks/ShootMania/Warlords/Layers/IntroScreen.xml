<?xml version="1.0" encoding="utf-8" standalone="yes" ?>
<manialink version="2" name="{( LayerName )}">

	<frame id="main" posn="0 0 -2" hidden="0">

		<frame id="leftpanel" posn="0 0 2">
			<label id="title" posn="-1 0" sizen="160 10" halign="right" valign="center" style="TextTitle3" text="WARLORDS" textsize="10" />
			<label id="subtitle" posn="-1 -8 1" sizen="160 10" halign="right" valign="center" style="TextTitle3" text="Loading" textsize="3" />
			<quad id="bg" posn="0 0 0" sizen="320 180" halign="center" valign="center" bgcolor="fff8" image="{( LeftPanelImageUrl )}" scriptevents="1" />
		</frame>

		<quad id="fog" posn="-70 0 1" sizen="100 100" image="{( FogImageUrl )}"  valign="center" halign="center"/>

		<frame id="rightpanel" posn="0 0 0">
			<quad id="bg" posn="0 0 0" sizen="320 180" halign="center" valign="center" bgcolor="fff8" image="{( RightPanelImageUrl )}" scriptevents="1" />
		</frame>
	</frame>

	<script><!--

		#Include "AnimLib" as AL

		#Const C_LeftPanelOpenedPosX	-320.
		#Const C_LeftPanelClosedPosX	0.
		#Const C_RightPanelOpenedPosX	320.
		#Const C_RightPanelClosedPosX	0.

		#Const	C_OpeningAnimDuration	{( AnimDuration )}
		#Const	C_FogAnimDuration		{( AnimDuration )}

		// ---------------------------------- //
		/**
		 * Main script.
		 */
		main() {

			declare netread	Boolean	Srv_UI_G_IntroScreenVisibility					for Teams[0];
			declare netread Text	Srv_UI_G_IntroScreenText						for Teams[0];

			declare Text LastIntroScreenText;
			declare Boolean	LastIntroScreenVisibility;
			declare Integer IntroScreenVisibilityTimestamp;


			declare CMlFrame	MainFrame		<=> (Page.GetFirstChild("main")				as CMlFrame);

			declare CMlFrame	LeftPanel		<=> (Page.GetFirstChild("leftpanel")		as CMlFrame);
			declare CMlQuad		LeftPanelBg		<=> (LeftPanel.GetFirstChild("bg")			as CMlQuad);

			declare CMlFrame	RightPanel		<=> (Page.GetFirstChild("rightpanel")		as CMlFrame);
			declare CMlQuad		RightPanelBg	<=> (RightPanel.GetFirstChild("bg")			as CMlQuad);

			declare CMlQuad		Fog				<=> (Page.GetFirstChild("fog")				as CMlQuad);

			declare CMlLabel	SubTitle		<=> (LeftPanel.GetFirstChild("subtitle")	as CMlLabel);

			while(True) {

				if (LastIntroScreenText != Srv_UI_G_IntroScreenText) {
					LastIntroScreenText = Srv_UI_G_IntroScreenText;
					SubTitle.SetText(LastIntroScreenText);
				}

				if (LastIntroScreenVisibility != Srv_UI_G_IntroScreenVisibility) {
					LastIntroScreenVisibility = Srv_UI_G_IntroScreenVisibility;
					IntroScreenVisibilityTimestamp = Now;
				}

				if (LastIntroScreenVisibility) {
					// Closing

					Fog.Hide();

					LeftPanel.RelativePosition.X = AL::EaseInSine(Now - IntroScreenVisibilityTimestamp, C_LeftPanelOpenedPosX, C_LeftPanelClosedPosX - C_LeftPanelOpenedPosX, C_OpeningAnimDuration);
					RightPanel.RelativePosition.X = AL::EaseInSine(Now - IntroScreenVisibilityTimestamp, C_RightPanelOpenedPosX, C_RightPanelClosedPosX - C_RightPanelOpenedPosX, C_OpeningAnimDuration);

					declare Real Opacity = AL::EaseLinear(Now - IntroScreenVisibilityTimestamp, 0., 1., C_OpeningAnimDuration);
					LeftPanelBg.Opacity = Opacity;
					RightPanelBg.Opacity = Opacity;
					SubTitle.Opacity = Opacity;

				} else {
					// Opening

					Fog.Show();
					Fog.RelativePosition.X = AL::EaseInOutSine(Now - IntroScreenVisibilityTimestamp, -70., 70., C_FogAnimDuration / 2);
					Fog.Opacity = AL::EaseOutExp(Now - IntroScreenVisibilityTimestamp, 1., -1., C_FogAnimDuration);
					Fog.RelativeScale = AL::EaseOutExp(Now - IntroScreenVisibilityTimestamp, 1., 2., C_FogAnimDuration / 2);
					Fog.RelativeRotation += Period / 100.;

					LeftPanel.RelativePosition.X = AL::EaseInSine(Now - IntroScreenVisibilityTimestamp - 1000, C_LeftPanelClosedPosX, C_LeftPanelOpenedPosX - C_LeftPanelClosedPosX, C_OpeningAnimDuration);
					RightPanel.RelativePosition.X = AL::EaseInSine(Now - IntroScreenVisibilityTimestamp - 1000, C_RightPanelClosedPosX, C_RightPanelOpenedPosX - C_RightPanelClosedPosX, C_OpeningAnimDuration);

					declare Real Opacity = AL::EaseLinear(Now - IntroScreenVisibilityTimestamp - 1000, 1., -1., C_OpeningAnimDuration);
					LeftPanelBg.Opacity = Opacity;
					RightPanelBg.Opacity = Opacity;
					SubTitle.Opacity = Opacity;

				}

				if (!MainFrame.Visible) MainFrame.Show();

				yield;
			}

		}

	--></script>
</manialink>