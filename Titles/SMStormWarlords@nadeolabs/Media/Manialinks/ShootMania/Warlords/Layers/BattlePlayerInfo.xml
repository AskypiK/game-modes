<manialink version="2" name="{( LayerName )}">
	<frame posn="83 -95">
		<label id="playername" posn="75 14 1" sizen="70 5" halign="right" valign="top" style="TextTitle3" textsize="3" />
		<label id="playergold" posn="75 5 1" sizen="70 5" halign="right" valign="bottom" style="TextTitle3" textcolor="fa0" textsize="2" />
		<quad id="bg" posn="0 0 -1" sizen="80 22" halign="left" valign="bottom" style="UiSMSpectatorScoreBig" substyle="UIRange2Bg" />
	</frame>
	<script><!--

		#Include "TextLib" as TL

		// ---------------------------------- //
		/**
		 * Gets the id of the given player.
		 *
		 * @param _Login The player's login to look for.
		 * @return The given player's id.
		 */
		Integer GetPlayerId(Text _Login) {
			declare netread Text[Integer] Srv_Player_G_Players for Teams[0];
			if(Srv_Player_G_Players.exists(_Login)) {
				return Srv_Player_G_Players.keyof(_Login);
			}
			
			return -1;
		}

		// ---------------------------------- //
		/**
		 * Gets the local player's id.
		 *
		 * @return The local player's id.
		 */
		Integer GetInputPlayerId() {
			return GetPlayerId(InputPlayer.User.Login);
		}

		Boolean IsSpec() {
			return (GetInputPlayerId() == -1);
		}

		// ---------------------------------- //
		/**
		 * Gets the label which contains the local player's name.
		 *
		 * @return The name label.
		 */
		CMlLabel GetPlayerNameLabel() {
			return (Page.GetFirstChild("playername") as CMlLabel);
		}
		
		// ---------------------------------- //
		/**
		 * Gets the label which contains the local player's gold.
		 *
		 * @return The gold label.
		 */
		CMlLabel GetPlayerGoldLabel() {
			return (Page.GetFirstChild("playergold") as CMlLabel);
		}
		
		// ---------------------------------- //
		/**
		 * Gets the local player's gold.
		 *
		 * @return The local player's gold.
		 */
		Integer GetInputPlayerGold() {
			declare netread Integer[Integer] Srv_Player_G_PlayersGold for Teams[0];
			return Srv_Player_G_PlayersGold[GetInputPlayerId()];
		}

		// ---------------------------------- //
		/**
		 * Changes the name label's content.
		 *
		 * @param _PlayerName The name to display.
		 */
		Void SetPlayerName(Text _PlayerName) {
			GetPlayerNameLabel().SetText(_PlayerName);
		}

		// ---------------------------------- //
		/**
		 * Changes the gold label's content.
		 *
		 * @param _Value The value to display.
		 */
		Void SetPlayerGold(Integer _Value) {
			GetPlayerGoldLabel().SetText(_Value ^ " G");
		}

		// ---------------------------------- //
		/**
		 * Main script.
		 */
		main() {

			while (InputPlayer == Null) yield;
			
			SetPlayerName(InputPlayer.User.Name);

			while (True) {
				
				if (!IsSpec()) SetPlayerGold(GetInputPlayerGold());

				yield;

			}

		}

	--></script>
</manialink>