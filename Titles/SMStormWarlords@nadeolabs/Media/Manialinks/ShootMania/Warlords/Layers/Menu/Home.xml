<manialink version="2" name="WLMenu" background="0">

	<frame posn="0 -20 3">
		<label id="play" posn="-45 0" sizen="60 10" halign="center" valign="center" style="TextButtonBig" textsize="4" text="{( Play )}" scriptevents="1" />
		<label id="galaxy-editor" posn="0 0" sizen="60 10" halign="center" valign="center" style="TextButtonBig" textsize="4" text="{( GalaxyEditor )}" scriptevents="1" />
		<label id="other-editors" posn="0 -12" sizen="60 10" halign="center" valign="center" style="TextButtonBig" textsize="4" text="{( OtherEditors )}" scriptevents="1" />
		<label id="quit" posn="45 0" sizen="60 10" halign="center" valign="center" style="TextButtonBig" textsize="4" text="{( Quit )}" scriptevents="1" />
	</frame>

	<quad id="bg" posn="0 10" sizen="96 48" halign="center" valign="center" style="TitleLogos" substyle="Title" />


	<script><!--

		#Include "AnimLib" as AL

		CMlQuad GetLogo() {
			return (Page.GetFirstChild("bg") as CMlQuad);
		}

		CMlLabel GetPlayLabel() {
			return (Page.GetFirstChild("play") as CMlLabel);
		}

		CMlLabel GetGalaxyEditorLabel() {
			return (Page.GetFirstChild("galaxy-editor") as CMlLabel);
		}

		CMlLabel GetOtherEditorsLabel() {
			return (Page.GetFirstChild("other-editors") as CMlLabel);
		}

		CMlLabel GetQuitLabel() {
			return (Page.GetFirstChild("quit") as CMlLabel);
		}

		Void Init() {
			declare Integer ScriptStart for This;
			ScriptStart = Now;
		}

		Void LoopStart() {

		}

		Void Animate() {
			declare Integer ScriptStart for This;

			declare CMlQuad Logo = GetLogo();
			declare CMlLabel Play = GetPlayLabel();
			declare CMlLabel GalaxyEditor = GetGalaxyEditorLabel();
			declare CMlLabel OtherEditors = GetOtherEditorsLabel();
			declare CMlLabel Quit = GetQuitLabel();

			Logo.RelativeScale = AL::EaseInOutSine(Now - ScriptStart, 1.2, -0.2, 2000);
			Logo.Opacity = AL::EaseInOutSine(Now - ScriptStart, 0., 1., 2000);

			Play.Opacity = AL::EaseInOutSine(Now - ScriptStart - 1000, 0., 1., 2000);
			GalaxyEditor.Opacity = AL::EaseInOutSine(Now - ScriptStart - 1200, 0., 1., 2000);
			OtherEditors.Opacity = AL::EaseInOutSine(Now - ScriptStart - 1400, 0., 1., 2000);
			Quit.Opacity = AL::EaseInOutSine(Now - ScriptStart - 1600, 0., 1., 2000);
		}

		main() {

			Init();

			while (True) {

				LoopStart();

				foreach (Event in PendingEvents) {
					switch (Event.Type) {
						case CMlEvent::Type::MouseClick: {
							switch (Event.ControlId) {
								case "play": {
									SendCustomEvent("play", Text[]);
								}
								case "galaxy-editor": {
									SendCustomEvent("edit-galaxy", Text[]);
								}
								case "other-editors": {
									SendCustomEvent("edit-others", Text[]);
								}
								case "quit": {
									SendCustomEvent("quit", Text[]);
								}
							}
						}
					}
				}

				Animate();
				yield;

			}
		}

	--></script>

</manialink>