<manialink version="2" name="{( LayerName )}">
	<framemodel id="attacker">
		<label id="name" posn="4 0 0" sizen="24 5" text="Player" />
		<quad id="emblem" posn="0 -2 0" sizen="6 6"  style="Emblems" substyle="#0" valign="center" halign="center" />
		<label id="gold" posn="40 0 0" sizen="10 5" text="" textcolor="fa0" halign="right" style="TextRaceChrono" textsize="4" />
	</framemodel>

	<framemodel id="defender">
		<label id="name" posn="4 0 0" sizen="24 5" text="Player" />
		<quad id="emblem" posn="0 -2 0" sizen="6 6"  style="Emblems" substyle="#0" valign="center" halign="center" />
		<label id="gold" posn="40 0 0" sizen="10 5" text="" textcolor="fa0" halign="right" style="TextRaceChrono" textsize="4" />
	</framemodel>

	<frame id="main" posn="0 -90" hidden="1">
		<frame posn="-30 0">
			<label posn="0 64 3" sizen="50 5" text="Attack" valign="center2" halign="center" style="TextTitle3" />
			<quad posn="0 65 2" sizen="50 10" style="Bgs1" substyle="Glow" valign="center" halign="center" />
			<quad posn="0 64 1" sizen="56 8" style="Bgs1" substyle="BgGlow2" valign="center" halign="center" />
			
			<frame id="attack" posn="-20 60 2">
				<frame id="attackers">
					<frameinstance modelid="attacker" posn="0 0" />
					<frameinstance modelid="attacker" posn="0 -8" />
					<frameinstance modelid="attacker" posn="0 -16" />
					<frameinstance modelid="attacker" posn="0 -24" />
					<frameinstance modelid="attacker" posn="0 -32" />
					<frameinstance modelid="attacker" posn="0 -40" />
				</frame>
				
				<label id="attack-sumtitle" posn="4 -50.5" sizen="24 5" style="TextTitle3" text="Sum"  valign="center2" textsize="3" />
				<label id="attack-sumgold" posn="40 -48" sizen="10 5" text="" textcolor="fa0" halign="right" style="TextRaceChrono" textsize="4" />
			</frame>
			
			<quad id="bg" posn="0 0 0" sizen="50 64" style="Bgs1" substyle="BgCardProperty" valign="bottom" halign="center" opacity="0.75" />
		</frame>
		
		<frame posn="30 0">
			<label posn="0 64 3" sizen="50 5" text="Defense" valign="center2" halign="center" style="TextTitle3" />
			<quad posn="0 65 2" sizen="50 10" style="Bgs1" substyle="Glow" valign="center" halign="center" />
			<quad posn="0 64 1" sizen="56 8" style="Bgs1" substyle="BgGlow2" valign="center" halign="center" />
			
			<frame id="defense" posn="-20 60 2">
				<frame id="defenders">
					<frameinstance modelid="defender" posn="0 0" />
					<frameinstance modelid="defender" posn="0 -8" />
					<frameinstance modelid="defender" posn="0 -16" />
					<frameinstance modelid="defender" posn="0 -24" />
					<frameinstance modelid="defender" posn="0 -32" />
					<frameinstance modelid="defender" posn="0 -40" />
				</frame>
				
				<label id="defense-sumtitle" posn="4 -50.5" sizen="24 5" style="TextTitle3" text="Sum"  textsize="3" valign="center2" />
				<label id="defense-sumgold" posn="40 -48" sizen="10 5" text="" textcolor="fa0" halign="right" style="TextRaceChrono" textsize="4" />
			</frame>
			
			<quad id="bg" posn="0 0 0" sizen="50 64" style="Bgs1" substyle="BgCardProperty" valign="bottom" halign="center" opacity="0.75" />
		</frame>
	</frame>

	<script><!--

		#Include "TextLib" as TL
		#Include "AnimLib" as AL

		#Const C_EmblemBaseUrl "{( EmblemBaseUrl )}"
		#Const C_OpenAnimDuration {( OpenAnimDuration )}

		CMlFrame GetMainFrame() {
			return (Page.GetFirstChild("main") as CMlFrame);
		}

		CMlFrame GetAttackerFrameInstancesParentFrame() {
			return (Page.GetFirstChild("attackers") as CMlFrame);
		}

		CMlFrame GetDefenderFrameInstancesParentFrame() {
			return (Page.GetFirstChild("defenders") as CMlFrame);
		}

		CMlFrame[] GetAttackerFrameInstances() {
			declare CMlFrame[] AttackerFrameInstances;
			foreach (Control in GetAttackerFrameInstancesParentFrame().Controls) {
				if (!(Control is CMlFrame)) continue;
				AttackerFrameInstances.add((Control as CMlFrame));
			}
			return AttackerFrameInstances;
		}

		CMlFrame[] GetDefenderFrameInstances() {
			declare CMlFrame[] DefenderFrameInstances;
			foreach (Control in GetDefenderFrameInstancesParentFrame().Controls) {
				if (!(Control is CMlFrame)) continue;
				DefenderFrameInstances.add((Control as CMlFrame));
			}
			return DefenderFrameInstances;
		}

		CMlFrame GetAttackerFrameInstance(Integer _Index) {
			return GetAttackerFrameInstances()[_Index];
		}

		CMlFrame GetDefenderFrameInstance(Integer _Index) {
			return GetDefenderFrameInstances()[_Index];
		}

		CMlLabel ADFI_GetNameLabel(CMlFrame _FrameInstance) {
			return (_FrameInstance.GetFirstChild("name") as CMlLabel);
		}

		CMlLabel ADFI_GetGoldLabel(CMlFrame _FrameInstance) {
			return (_FrameInstance.GetFirstChild("gold") as CMlLabel);
		}

		CMlQuad ADFI_GetEmblemQuad(CMlFrame _FrameInstance) {
			return (_FrameInstance.GetFirstChild("emblem") as CMlQuad);
		}

		CMlLabel GetAttackGoldSumLabel() {
			return (Page.GetFirstChild("attack-sumgold") as CMlLabel);
		}

		CMlLabel GetDefenseGoldSumLabel() {
			return (Page.GetFirstChild("defense-sumgold") as CMlLabel);
		}

		Void HideAllAttackerFrameInstances() {
			foreach (AttackerFrameInstance in GetAttackerFrameInstances()) {
				AttackerFrameInstance.Hide();
			}
		}

		Void HideAllDefenderFrameInstances() {
			foreach (DefenderFrameInstance in GetDefenderFrameInstances()) {
				DefenderFrameInstance.Hide();
			}
		}

		Void SetAttackGoldSum(Integer _Amount) {
			GetAttackGoldSumLabel().SetText(TL::ToText(_Amount));
		}

		Void SetDefenseGoldSum(Integer _Amount) {
			GetDefenseGoldSumLabel().SetText(TL::ToText(_Amount));
		}

		Text GetPlayerLogin(Integer _PlayerId) {
			declare netread Text[Integer] Srv_Player_G_Players for Teams[0];
			return Srv_Player_G_Players[_PlayerId];
		}

		Text GetPlayerName(Text _Login) {
			foreach (Player in Players) {
				if (Player.User.Login == _Login) return Player.User.Name;
			}
			return _Login;
		}

		Text GetEmblemUrl(Integer _OwnerId) {
			return C_EmblemBaseUrl ^ _OwnerId + 1 ^ ".dds";
		}

		Void Init() {
			while (InputPlayer == Null) { yield; }
			while (InputPlayer.User == Null) { yield; }

			declare Integer ScriptStart for This;
			ScriptStart = Now;
		}

		Void LoopStart() {
			declare netread Int3[][] Srv_UI_G_AttackRecapPlayers for Teams[0];
			declare Int3[] Attackers = Srv_UI_G_AttackRecapPlayers[0];
			declare Int3[] Defenders = Srv_UI_G_AttackRecapPlayers[1];

			HideAllAttackerFrameInstances();

			declare Integer AttackGoldSum;
			foreach (Index => AtkGold in Attackers) {

				declare Integer AttackerId = AtkGold.X;
				declare Integer GoldAmount = AtkGold.Y;
				
				declare CMlFrame	AttackerFrameInstance	= GetAttackerFrameInstance(Index);
				declare CMlLabel	AttackerNameLabel		= ADFI_GetNameLabel(AttackerFrameInstance);
				declare CMlLabel	AttackerGoldLabel		= ADFI_GetGoldLabel(AttackerFrameInstance);
				declare CMlQuad		AttackerEmblemQuad		= ADFI_GetEmblemQuad(AttackerFrameInstance);

				AttackerNameLabel.SetText(GetPlayerName(GetPlayerLogin(AttackerId)));
				AttackerGoldLabel.SetText(TL::ToText(GoldAmount));
				AttackerEmblemQuad.ImageUrl = GetEmblemUrl(AttackerId);
				AttackerFrameInstance.Show();

				AttackGoldSum += GoldAmount;
			}

			SetAttackGoldSum(AttackGoldSum);

			HideAllDefenderFrameInstances();

			declare Integer DefenseGoldSum;
			foreach (Index => DefGold in Defenders) {

				declare Integer DefenderId = DefGold.X;
				declare Integer GoldAmount = DefGold.Y;
				
				declare CMlFrame	DefenderFrameInstance	= GetDefenderFrameInstance(Index);
				declare CMlLabel	DefenderNameLabel		= ADFI_GetNameLabel(DefenderFrameInstance);
				declare CMlLabel	DefenderGoldLabel		= ADFI_GetGoldLabel(DefenderFrameInstance);
				declare CMlQuad		DefenderEmblemQuad		= ADFI_GetEmblemQuad(DefenderFrameInstance);

				DefenderNameLabel.SetText(GetPlayerName(GetPlayerLogin(DefenderId)));
				DefenderGoldLabel.SetText(TL::ToText(GoldAmount));
				DefenderEmblemQuad.ImageUrl = GetEmblemUrl(DefenderId);
				DefenderFrameInstance.Show();

				DefenseGoldSum += GoldAmount;
			}

			SetDefenseGoldSum(DefenseGoldSum);
		}

		Void Animate() {
			declare Integer ScriptStart for This;

			declare CMlFrame MainFrame = GetMainFrame();
			MainFrame.Show();
			MainFrame.RelativePosition.Y = AL::EaseOutSine(Now - ScriptStart, -154., 64., C_OpenAnimDuration); // = -90.
		}

		main() {
	
			Init();

			while (True) {

				LoopStart();

				Animate();
				yield;
			}

		}

	--></script>
</manialink>