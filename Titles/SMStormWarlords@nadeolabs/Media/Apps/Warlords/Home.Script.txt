#RequireContext CManiaAppTitle
#Include "Libs/Nadeo/ShootMania/Warlords/Menu/Editor.Script.txt"	as Editor
#Include "Libs/Nadeo/Uized.Script.txt"								as Uized

#Const	C_BackgroundMlUrl	"file://Media/Manialinks/ShootMania/Warlords/Layers/Menu/Background.xml"
#Const	C_BackgroundId		"Background"
#Const	C_HomeMlUrl			"file://Media/Manialinks/ShootMania/Warlords/Layers/Menu/Home.xml"
#Const	C_HomeId			"Home"

main() {

	log(Now ^ "> Launched!");

	while (True) {

		while (!IsVisible) {
			yield;
			log("Pending...");
		}

		declare CUILayer Background = UILayerCreate();
		Background.AttachId = C_BackgroundId;
		Background.ManialinkPage = Uized::Render(C_BackgroundMlUrl);

		declare CUILayer Home = UILayerCreate();
		Home.AttachId = C_HomeId;
		Uized::Prepare("Play", _("Play"));
		Uized::Prepare("GalaxyEditor", _("Galaxy editor"));
		Uized::Prepare("OtherEditors", _("Other editors"));
		Uized::Prepare("Quit", _("Quit"));
		Home.ManialinkPage = Uized::Render(C_HomeMlUrl);

		declare Boolean Continue = True;

		while (Continue) {

			foreach (Event in PendingEvents) {
				switch (Event.Type) {
					case CManiaAppEvent::EType::LayerCustomEvent: {
						switch (Event.CustomEventType) {
							case "play": {
								Audio.PlaySoundEvent(CAudioManager::ELibSound::Valid, 0, 0.);
								Menu_Local();
							}
							case "edit-galaxy": {
								Audio.PlaySoundEvent(CAudioManager::ELibSound::Valid, 0, 0.);
								Home.IsVisible = False;
								Editor::Launch();
								Home.IsVisible = True;
							}
							case "edit-others": {
								Audio.PlaySoundEvent(CAudioManager::ELibSound::Valid, 0, 0.);
								Menu_Editor();
							}
							case "quit": {
								Audio.PlaySoundEvent(CAudioManager::ELibSound::Valid, 0, 0.);
								Continue = False;
							}
						}
					}
				}
			}

			yield;

		}

		Menu_Quit();
		UILayerDestroy(Background);
		UILayerDestroy(Home);
	}
}