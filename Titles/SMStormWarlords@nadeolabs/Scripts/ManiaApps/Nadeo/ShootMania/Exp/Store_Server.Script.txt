/**
 *	Display store UI
 */
#Const Version		"2016-11-10"
#Const ScriptName	"ManiaApps/Nadeo/ShootMania/Exp/Store_Server.Script.txt"

// ---------------------------------- //
// Functions
// ---------------------------------- //
// ---------------------------------- //
// Public
// ---------------------------------- //
// ---------------------------------- //
/** Return the version number of the script
 *
 *  @return   The version number of the script
 */
Text GetScriptVersion() {
  return Version;
}

// ---------------------------------- //
/** Return the name of the script
 *
 *  @return   The name of the script
 */
Text GetScriptName() {
  return ScriptName;
}

// ---------------------------------- //
/** Set the open state of the store of
 *	the given player
 *
 *	@param	_Player										The player to update
 *	@param	_Open											True if the store is open,
 *																		False if the store is closed
 */
Void SetOpenState(CSmPlayer _Player, Boolean _Open) {
	if (_Player == Null) return;
	declare UI <=> UIManager.GetUI(_Player);
	if (UI != Null) {
		declare netwrite Net_LibStoreExp_StoreIsOpen for UI = False;
		Net_LibStoreExp_StoreIsOpen = _Open;
	}
}

// ---------------------------------- //
/** Check if the open button was pressed
 *	by the given player
 *
 *	@param	_Player										The player to check
 *
 *	@return														True if the button was pressed,
 *																		False otherwise
 */
Boolean OpenButtonPressed(CSmPlayer _Player) {
	if (_Player == Null) return False;
	declare UI <=> UIManager.GetUI(_Player);
	if (UI != Null) {
		declare netread Net_LibStoreExp_OpenButtonPressed for UI = 0;
		declare LibStoreExp_PrevOpenButtonPressed for UI = 0;
		if (LibStoreExp_PrevOpenButtonPressed != Net_LibStoreExp_OpenButtonPressed) {
			LibStoreExp_PrevOpenButtonPressed = Net_LibStoreExp_OpenButtonPressed;
			return True;
		}
	}
	return False;
}

// ---------------------------------- //
/** Check if the reset button was pressed
 *	by the given player
 *
 *	@param	_Player										The player to check
 *
 *	@return														True if the button was pressed,
 *																		False otherwise
 */
Boolean ResetButtonPressed(CSmPlayer _Player) {
	if (_Player == Null) return False;
	declare UI <=> UIManager.GetUI(_Player);
	if (UI != Null) {
		declare netread Net_LibStoreExp_ResetButtonPressed for UI = 0;
		declare LibStoreExp_PrevResetButtonPressed for UI = 0;
		if (LibStoreExp_PrevResetButtonPressed != Net_LibStoreExp_ResetButtonPressed) {
			LibStoreExp_PrevResetButtonPressed = Net_LibStoreExp_ResetButtonPressed;
			return True;
		}
	}
	return False;
}

// ---------------------------------- //
/** Hide or display the store buttons
 *	(reset and open buttons)
 *
 *	@param	_Player										The player to update
 *	@param	_Visible									True to show, False to hide
 */
Void SetButtonsVisibility(CSmPlayer _Player, Boolean _Visible) {
	if (_Player == Null) return;
	declare UI <=> UIManager.GetUI(_Player);
	if (UI == Null) return;
	
	declare netwrite Net_LibStoreExp_StoreVisibility for UI = False;
	Net_LibStoreExp_StoreVisibility = _Visible;
}

// ---------------------------------- //
/// Unload the library
Void Unload() {
	
}

// ---------------------------------- //
/// Load the library
Void Load() {
	Unload();
}