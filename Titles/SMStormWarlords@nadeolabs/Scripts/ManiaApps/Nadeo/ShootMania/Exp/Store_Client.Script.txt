/**
 *	Display store UI
 */
#Const Version		"2016-11-10"
#Const ScriptName	"ManiaApps/Nadeo/ShootMania/Exp/Store_Client.Script.txt"

// ---------------------------------- //
// Libraries
// ---------------------------------- //
#Include "ManiaApps/Nadeo/Layers.Script.txt" as Layers

// ---------------------------------- //
// Constants
// ---------------------------------- //
#Const C_LayerName "EXP_Store"

// ---------------------------------- //
// Globales
// ---------------------------------- //
declare Boolean G_CanDisplayStoreUI;

// ---------------------------------- //
// Functions
// ---------------------------------- //
// ---------------------------------- //
// Public
// ---------------------------------- //
// ---------------------------------- //
/** Return the version number of the script
 *
 *  @return   The version number of the script
 */
Text GetScriptVersion() {
  return Version;
}

// ---------------------------------- //
/** Return the name of the script
 *
 *  @return   The name of the script
 */
Text GetScriptName() {
  return ScriptName;
}

// ---------------------------------- //
/** Get the store manialink
 *
 *  @return                           The manialink
 */
Text GetUILayerStore() {
	return """
<manialink name="{{{C_LayerName}}}" version="3">
<frame z-index="10" id="frame-global">
	<frame pos="-30 -67" id="frame-buttons">
		<label halign="right" style="CardButtonMediumXL" text="{{{_("Open store")}}}" scriptevents="1" id="button-open" />
		<label halign="right" style="CardButtonMediumXL" text="{{{_("Reset store")}}}" hidden="1" scriptevents="1" id="button-reset" />
	</frame>
</frame>
<script><!--
main() {
	declare Frame_Buttons <=> (Page.GetFirstChild("frame-buttons") as CMlFrame);
	declare Button_Open <=> (Frame_Buttons.GetFirstChild("button-open") as CMlLabel);
	declare Button_Reset <=> (Frame_Buttons.GetFirstChild("button-reset") as CMlLabel);

	Button_Reset.Visible = False;

	declare netread Net_LibStoreExp_StoreIsOpen for UI = False;
	declare netwrite Net_LibStoreExp_OpenButtonPressed for UI = 0;
	declare netwrite Net_LibStoreExp_ResetButtonPressed for UI = 0;

	declare PrevStoreIsOpen = False;

	while (True) {
		yield;
		if (!PageIsVisible) continue;
		
		if (PrevStoreIsOpen != Net_LibStoreExp_StoreIsOpen) {
			PrevStoreIsOpen = Net_LibStoreExp_StoreIsOpen;

			Button_Open.Visible = !Net_LibStoreExp_StoreIsOpen;
			//Button_Reset.Visible = Net_LibStoreExp_StoreIsOpen;
		}

		foreach (Event in PendingEvents) {
			if (Event.Type == CMlScriptEvent::Type::MouseClick) {
				if (Event.ControlId == "button-open") {
					Net_LibStoreExp_OpenButtonPressed = Now;
				} else if (Event.ControlId == "button-reset") {
					Net_LibStoreExp_ResetButtonPressed = Now;
				}
			}
		}
	}
}
--></script>
</manialink>
	""";
}

// ---------------------------------- //
/// Update the library
Void Yield() {
	declare netread Net_LibStoreExp_StoreVisibility for UI = False;
	if (G_CanDisplayStoreUI != Net_LibStoreExp_StoreVisibility) {
		G_CanDisplayStoreUI = Net_LibStoreExp_StoreVisibility;
		Layers::SetVisibility(C_LayerName, G_CanDisplayStoreUI);
	}
}

// ---------------------------------- //
/// Unload the library
Void Unload() {
	Layers::Destroy(C_LayerName);
	G_CanDisplayStoreUI = False;
}

// ---------------------------------- //
/// Load the library
Void Load() {
	Unload();
	Layers::Create(C_LayerName, GetUILayerStore());
	Layers::SetAnimationType(C_LayerName, CUILayer::EUILayerAnimation::DownFast);
	Layers::SetVisibility(C_LayerName, G_CanDisplayStoreUI);
}
