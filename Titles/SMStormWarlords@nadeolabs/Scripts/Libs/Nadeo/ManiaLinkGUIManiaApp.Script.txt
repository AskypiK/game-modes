#Const C_LayerName	"ManiaLinkGUI"

declare CUILayer G_Layer;

Text GetML() {
	return """
<manialink version="2" background="0" name="{{{ C_LayerName }}}">

	<label id="notification" posn="0 0 60" style="TextTitle3" text="Press 'N' to display the script view" halign="center" valign="center" hidden="1" />

	<frame id="mainframe" posn="0 0 60">
		<textedit id="script" posn="0 0 1" sizen="284 158" halign="center" valign="center" textformat="script" style="StyleTextScriptEditor" />
		<quad posn="0 0 0" style="Bgs1" substyle="BgDialogBlur" sizen="288 162" halign="center" valign="center" />
	</frame>

	<script><!--

		main() {

			declare Text Net_MLGUI_Content for LocalUser;
			declare Boolean Net_Visibility for LocalUser = False;
			declare Boolean PreviousVisibility = Net_Visibility;
			declare Boolean RequestedVisiblity = False;

			declare CMlFrame MainFrame		<=> (Page.GetFirstChild("mainframe") as CMlFrame);
			declare CMlLabel Notification	<=> (Page.GetFirstChild("notification") as CMlLabel);
			declare CMlTextEdit ScriptView	<=> (Page.GetFirstChild("script") as CMlTextEdit);

			while (True) {

				yield;

				MainFrame.Visible = RequestedVisiblity;
				
				if (PreviousVisibility != Net_Visibility) {
					PreviousVisibility = Net_Visibility;
					if (Net_Visibility && !RequestedVisiblity) Notification.Show();
				}

				foreach (Event in PendingEvents) {
					if (Event.Type == CMlEvent::Type::KeyPress && Event.KeyName == "N") {
						RequestedVisiblity = !RequestedVisiblity;
						ScriptView.Value = Net_MLGUI_Content;
						Notification.Hide();
					}
				}

			}

		}

	--></script>

</manialink>
	""";
}

Void ShowScriptView(Text _Content) {

	declare Text Net_MLGUI_Content for LocalUser;
	Net_MLGUI_Content = _Content;

	declare Boolean Net_Visibility for LocalUser;
	Net_Visibility = True;
}

Void HideScriptView() {
	declare Boolean Net_Visibility for LocalUser;
	Net_Visibility = False;
}

Void Load() {
	G_Layer = UILayerCreate();
	G_Layer.ManialinkPage = GetML();

	HideScriptView();
}