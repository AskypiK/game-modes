/**
 * Chase winners UI
 */
#Const Version    "2018-06-05"
#Const ScriptName "ManiaApps/Nadeo/TrackMania/Chase/Winners_Client.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Libraries
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Include "TextLib" as TL
#Include "ManiaApps/Nadeo/Layers.Script.txt" as Layers

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Constants
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Const C_Layer_Winners "ChaseAttack_Winners"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Private
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Get the manialink of the Chase UI
 *
 *  @return                           Chase UI manialink
 */
Text Private_GetWinnersML() {
	//L16N [Chase Attack] Text displayed above the name of the players who won the match.
	declare Text_Winners = _("|Chase|Winners");
	declare NamesML = "";
	for (I, 0, 9) {
		declare PosY = I * -10;
		NamesML ^= """<label pos="0 {{{PosY}}}" valign="center2" textsize="8" class="text-default" />""";
	}

	return """
<manialink version="3" name="{{{C_Layer_Winners}}}">
<stylesheet>
  <style class="text-default" textfont="Oswald" textcolor="ffffff" textsize="3" textemboss="1" />
</stylesheet>
<frame hidden="1" id="frame-global">
	<frame pos="0 60" id="frame-winners">
		<label pos="-39 0" textfont="Oswald" textsize="12" textemboss="1" text="{{{Text_Winners}}}" />
		<frame pos="0 -8">
			<quad pos="0 -4.5" z-index="1" size="80 0.666" bgcolor="FFFFFF" halign="center" valign="center"/>
			<quad pos="35 -5" z-index="1" size="10 1" bgcolor="FFFFFF" halign="center" valign="center"/>
		</frame>
		<frame pos="-39 -20" id="frame-names">
			{{{NamesML}}}
		</frame>
	</frame>
</frame>
<script><!--
main() {
	declare Frame_Global <=> (Page.GetFirstChild("frame-global") as CMlFrame);
	declare Frame_Winners <=> (Frame_Global.GetFirstChild("frame-winners") as CMlFrame);
	declare Frame_Names <=> (Frame_Global.GetFirstChild("frame-names") as CMlFrame);

	declare netread Net_ChaseWinners_IsEnabled for Teams[0] = True;
	declare netread Net_ChaseWinners_WinnerNames for Teams[0] = Text[];
	declare netread Net_ChaseWinners_WinnerNamesUpdate for Teams[0] = -1;

	declare PrevPlayerNamesUpdate = -123;
	declare PrevVisible = Frame_Global.Visible;

	while (True) {
		yield;

		if (PrevVisible != Net_ChaseWinners_IsEnabled) {
			PrevVisible = Net_ChaseWinners_IsEnabled;
			Frame_Global.Visible = PrevVisible;
		}

		if (Frame_Global.Visible) {
			if (PrevPlayerNamesUpdate != Net_ChaseWinners_WinnerNamesUpdate) {
				PrevPlayerNamesUpdate = Net_ChaseWinners_WinnerNamesUpdate;

				foreach (Key => Control in Frame_Names.Controls) {
					declare Label_Name <=> (Control as CMlLabel);
					if (Net_ChaseWinners_WinnerNames.existskey(Key)) {
						Label_Name.Value = Net_ChaseWinners_WinnerNames[Key];
						Label_Name.Visible = True;
					} else {
						Label_Name.Visible = False;
					}
				}
			}
		}
	}
}
--></script>
</manialink>
""";
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Public
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Unload the library
Void Unload() {

}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Load the library
Void Load() {
	Unload();

	Layers::Create(C_Layer_Winners, Private_GetWinnersML());
}