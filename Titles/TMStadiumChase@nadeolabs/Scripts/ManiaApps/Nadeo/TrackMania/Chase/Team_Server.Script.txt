/**
 * Chase team UI
 */
#Const Version    "2018-06-05"
#Const ScriptName "ManiaApps/Nadeo/TrackMania/Chase/Team_Server.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Public
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Allow the display of team UI on the
 *	server or not
 *
 *	@param	_Enabled									True to display the team UI
 *																		to the players on this server
 *																		False otherwise
 */
Void Enable(Boolean _Enabled) {
	declare netwrite Net_ChaseTeam_IsEnabled for Teams[0] = True;
	Net_ChaseTeam_IsEnabled = _Enabled;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Update the names of the players in
 *	the team of the given player
 *
 *	@param	_Players									The players in the team
 *	@param	_Player										The player to update
 */
Void SetTeam(CTmPlayer[] _Players, CTmPlayer _Player) {
	if (_Player == Null) return;
	
	declare Names = Text[];
	foreach (Player in _Players) {
		if (Player != Null) {
			Names.add(Player.User.Name);
		}
	}
	
	declare netwrite Net_ChaseTeam_PlayerNames for _Player = Text[];
	declare netwrite Net_ChaseTeam_PlayerNamesUpdate for _Player = -1;
	Net_ChaseTeam_PlayerNames = Names;
	Net_ChaseTeam_PlayerNamesUpdate = Now;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Reset the names of the players  in
 *	the team of the given player
 *
 *	@param	_Player										The player to reset
 */
Void ResetTeam(CTmPlayer _Player) {
	SetTeam(CTmPlayer[], _Player);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Unload the library
Void Unload() {
	Enable(False);
	
	foreach (Player in AllPlayers) {
		ResetTeam(Player);
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Load the library
Void Load() {
	Unload();
}