/**
 * Chase relay UI
 */
#Const Version    "2018-06-25"
#Const ScriptName "ManiaApps/Nadeo/TrackMania/Chase/Relay_Client.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Libraries
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Include "TextLib" as TL
#Include "ManiaApps/Nadeo/Layers.Script.txt" as Layers

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Constants
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Const C_Layer_Relay "ChaseAttack_Relay"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Private
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Get the manialink of the Chase UI
 *
 *  @return                           Chase UI manialink
 */
Text Private_GetRelayML() {
	declare Text_RelayFailed = _("|Chase mode|Relay failed");
	declare Text_WaitForTeammates = _("|Chase mode|Wait for your teammates");
	

	return """
<manialink version="3" name="{{{C_Layer_Relay}}}">
<stylesheet>
  <style class="text-default" textfont="Oswald" textcolor="ffffff" textsize="3" textemboss="1" />
</stylesheet>
<frame hidden="1" id="frame-global">
	<frame pos="0 0" id="frame-restart" halign="center">
		<label id="label-name" size="0 6" halign="center" textsize="8" textcolor="FF0000" class="text-default" text="{{{Text_RelayFailed}}}" />
		<frame pos="0 -2">
			<quad id="label-underline" pos="0 -8.5" z-index="1" size="100 0.666" bgcolor="FF0000" halign="center" valign="center"/>
			<quad id="label-underunderline" pos="0 -9.333" z-index="1" size="10 1" bgcolor="FF0000" halign="center" valign="center"/>
		</frame>
		<label pos="0 -12.5" size="0 6" halign="center" textsize="8" textcolor="FF0000" text="{{{Text_WaitForTeammates}}}" class="text-default" id="label-wait-for-teammates" />
	</frame>
</frame>
<script><!--

CTmMlPlayer GetOwner() {
  if (GUIPlayer != Null) return GUIPlayer;
  return InputPlayer;
}

main() {
	declare Frame_Global <=> (Page.GetFirstChild("frame-global") as CMlFrame);
	declare Label_WaitForTeammates <=> (Frame_Global.GetFirstChild("label-wait-for-teammates") as CMlLabel);

	declare PrevHideTime = -123;

	while (True) {
		yield;

		declare Owner <=> GetOwner();
		if (Owner != Null) {
			declare netread Net_ChaseAttack_Relay_HideTime for Owner = -1;
			if (PrevHideTime != Net_ChaseAttack_Relay_HideTime) {
				PrevHideTime = Net_ChaseAttack_Relay_HideTime;

				if (PrevHideTime > GameTime) {
					declare netread Net_ChaseAttack_Relay_DisplayWaitForTeammates for Owner = False;
					Label_WaitForTeammates.Visible = Net_ChaseAttack_Relay_DisplayWaitForTeammates;
					Frame_Global.Visible = True;
				}
			}
		}

		if (Frame_Global.Visible && GameTime >= PrevHideTime) {
			Frame_Global.Visible = False;
		}
	}
}
--></script>
</manialink>
""";
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Public
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Unload the library
Void Unload() {

}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Load the library
Void Load() {
	Unload();

	Layers::Create(C_Layer_Relay, Private_GetRelayML());
}