/**
 * Chase tutorial UI
 */
#Const Version    "2018-06-20"
#Const ScriptName "ManiaApps/Nadeo/TrackMania/Chase/ChaseTuto_Server.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Public
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Allow the display of the tuto on the
 *	server or not
 *
 *	@param	_Enabled									True to enable the tutorial to be displayed
 *																		to the players on this server
 *																		False otherwise
 */
Void Enable(Boolean _Enabled) {
	declare netwrite Net_ChaseTuto_IsEnabled for Teams[0] = True;
	Net_ChaseTuto_IsEnabled = _Enabled;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Check if the given player is currently
 *	viewing the chase tutorial UI
 *
 *	@param	_Player											The player to check
 *
 *	@return															True if the player is viewing the tutorial
 *																			False otherwise
 */
Boolean IsViewingTutorial(CTmPlayer _Player) {
	if (_Player == Null) return False;
	declare UI <=> UIManager.GetUI(_Player);
	if (UI == Null) return False;
	
	declare netread Net_ChaseTuto_IsViewingTuto for UI = False;
	return Net_ChaseTuto_IsViewingTuto;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Send the lobby presence of a player
 *	to its UI
 *
 *	@param	_IsInLobby								True if the player is in the lobby
 *																		False otherwise
 *	@param	_Player										The player to update
 */
Void IsInLobby(Boolean _IsInLobby, CTmPlayer _Player) {
	if (_Player == Null) return;
	declare UI <=> UIManager.GetUI(_Player);
	if (UI == Null) return;
	
	declare netwrite Net_ChaseTuto_IsInLobby for UI = False;
	Net_ChaseTuto_IsInLobby = _IsInLobby;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Initialize the given player
 *
 *	@param	_Player										The player to initialize
 */
Void InitializePlayer(CTmPlayer _Player) {
	IsInLobby(False, _Player);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Unload the library
Void Unload() {
	Enable(False);
	foreach (Player in AllPlayers) {
		InitializePlayer(Player);
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/// Load the library
Void Load() {
	Unload();
}